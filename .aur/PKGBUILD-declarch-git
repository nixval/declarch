# Maintainer: nixval <nicovaliantoku@gmail.com>

pkgname=declarch-git
pkgver=r0.4.0.r0.gb8698c4
pkgrel=1
pkgdesc="A declarative package manager for Linux (git version, supports AUR, Flatpak, Soar)"
arch=('x86_64')
url="https://github.com/nixval/declarch"
license=('MIT')

depends=('gcc-libs' 'pacman' 'git')
makedepends=('cargo')
optdepends=(
  'paru: AUR backend for syncing'
  'yay: Alternative AUR helper'
  'flatpak: For managing Flatpak applications'
)

provides=('declarch')
conflicts=('declarch' 'declarch-bin')

source=("git+https://github.com/nixval/declarch.git#branch=main")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  cargo build --release --frozen --all-features
}

check() {
  cd "${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd "${pkgname%-git}"
  install -Dm755 "target/release/declarch" "$pkgdir/usr/bin/declarch"

  # Install shell completions (generated by declarch binary)
  install -Dm644 <(target/release/declarch completions bash) \
    "$pkgdir/usr/share/bash-completion/completions/declarch"
  install -Dm644 <(target/release/declarch completions zsh) \
    "$pkgdir/usr/share/zsh/site-functions/_declarch"
  install -Dm644 <(target/release/declarch completions fish) \
    "$pkgdir/usr/share/fish/vendor_completions.d/declarch".fish

  # Install documentation
  install -Dm644 README.md "$pkgdir/usr/share/doc/${pkgname}/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
