# Maintainer: nixval <nicovaliantoku@gmail.com>

pkgname=declarch
pkgver=0.4.0
pkgrel=1
pkgdesc="A declarative package manager for Linux (supports AUR, Flatpak, Soar)"
arch=('x86_64')
url="https://github.com/nixval/declarch"
license=('MIT')
depends=('pacman' 'git')
optdepends=(
  'paru: AUR backend for syncing'
  'yay: Alternative AUR helper'
  'flatpak: For managing Flatpak applications'
)
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('a63fe4e2f6d18179c9e2c22a345c062da9af12fdbd7d4d1b560702d2c5fb40a5')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Install shell completions (generated by declarch binary)
  install -Dm644 <(target/release/$pkgname completions bash) \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 <(target/release/$pkgname completions fish) \
    "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname".fish
  install -Dm644 <(target/release/$pkgname completions zsh) \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
